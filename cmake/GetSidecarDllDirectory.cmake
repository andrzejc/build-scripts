if(DEFINED _DllLocationFromLibIncluded)
    return()
endif()
set(_DllLocationFromLibIncluded 1)

function(get_sidecar_dll_directory var lib_path)
    get_filename_component(lib_name "${lib_path}" NAME)
    get_filename_component(lib_dir "${lib_path}" DIRECTORY)
    string(REGEX REPLACE "\\.[lL][iI][bB]$" ".dll" dll_name "${lib_name}")
    foreach(suff "" /bin /.. /../bin)
        if(EXISTS "${lib_dir}${suff}/${dll_name}")
            set("${var}" "${lib_dir}${suff}" PARENT_SCOPE)
            return()
        endif()
    endforeach()
    set("${var}" "${var}-NOTFOUND" PARENT_SCOPE)
endfunction()

function(setup_library_dll_directory target)
    if(NOT WIN32)
        return()
    endif()
    get_target_property(lib_path "${target}" IMPORTED_LOCATION)
    get_sidecar_dll_directory(dll_dir "${lib_path}")
    if(dll_dir)
        set_target_properties("${target}" PROPERTIES DLL_DIRECTORY "${dll_dir}")
    endif()
endfunction()
